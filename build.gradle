plugins {
	id 'java'
}

repositories {
	mavenLocal()
	maven {
		url = 'https://repo.runelite.net'
	}
	mavenCentral()
}

def runeLiteVersion = 'latest.release'

dependencies {
    compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion
    compileOnly 'org.projectlombok:lombok:1.18.20'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    
    compileOnly 'org.lwjgl:lwjgl:3.3.1'
    compileOnly 'org.lwjgl:lwjgl-opengl:3.3.1'
    
    testImplementation 'junit:junit:4.12'
    testImplementation (group: 'net.runelite', name:'client', version: runeLiteVersion) {
        exclude group: 'org.lwjgl', module: 'lwjgl'
        exclude group: 'org.lwjgl', module: 'lwjgl-opengl'
    }
    testImplementation (group: 'net.runelite', name:'jshell', version: runeLiteVersion)
    
    // Only include Windows natives for testing
    testRuntimeOnly 'org.lwjgl:lwjgl:3.3.2:natives-windows'
    testRuntimeOnly 'org.lwjgl:lwjgl-opengl:3.3.2:natives-windows'

    // Needed to run RuneLite from this project (run.bat calls the run task)
    runtimeOnly(group: 'net.runelite', name: 'client', version: runeLiteVersion) {
        // RuneLite's Gradle metadata can request natives for multiple platforms.
        // For this Windows-only runner, exclude LWJGL and add Windows artifacts explicitly.
        exclude group: 'org.lwjgl'
    }
    runtimeOnly 'org.lwjgl:lwjgl:3.3.2'
    runtimeOnly 'org.lwjgl:lwjgl-opengl:3.3.2'
    runtimeOnly 'org.lwjgl:lwjgl:3.3.2:natives-windows'
    runtimeOnly 'org.lwjgl:lwjgl-opengl:3.3.2:natives-windows'
}

group = 'com.joelhalen.droptracker'
version = '5.2.5'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.release.set(11)
}

tasks.register('runRuneLite', JavaExec) {
    group = 'runelite'
    description = 'Run RuneLite with this plugin on the classpath'

    // RuneLite's main entrypoint
    mainClass.set('net.runelite.client.RuneLite')

    // Include plugin classes/resources + RuneLite client + runtime deps
    classpath = sourceSets.main.runtimeClasspath

    // Helpful for local debugging; can be removed if undesired
    jvmArgs = [
        '-ea'
    ]
}

